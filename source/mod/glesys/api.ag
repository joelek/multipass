# ==============================================================================

guard DomainPrice: {
	"amount": number,
	"currency": string,
	"years": number
};

guard RegistrarInfo: {
	"autorenew": string,
	"state": string,
	"statedescription"?: string,
	"expire"?: string,
	"tld"?: string,
	"invoicenumber"?: string
};

guard Domain: {
	"domainname": string,
	"available"?: boolean,
	"createtime"?: string,
	"displayname"?: string,
	"expire"?: number,
	"minimum"?: number,
	"prices"?: DomainPrice[],
	"primarynameserver"?: string,
	"recordcount"?: number,
	"refresh"?: number,
	"registrarinfo"?: RegistrarInfo | null,
	"responsibleperson"?: string,
	"retry"?: number,
	"ttl"?: number,
	"usingglesysnameserver"?: string
};

route listDomains(): POST:/domain/list
	<=
	=> {
		"response": {
			"domains": Domain[]
		}
	};

# ==============================================================================

guard DomainRecordHost: "@" | string;

guard DomainRecordType: "A" | "NS" | "MX" | "TXT" | string;

guard DomainRecord: {
	"domainname": string,
	"data": string,
	"host": DomainRecordHost,
	"recordid": number,
	"ttl": number,
	"type": DomainRecordType
};

route listDomainRecords(): POST:/domain/listrecords
	<= {
		"domainname": string
	}
	=> {
		"response": {
			"records": DomainRecord[]
		}
	};

route createDomainRecord(): POST:/domain/addrecord
	<= {
		"domainname": string,
		"data": string,
		"host": DomainRecordHost,
		"type": DomainRecordType,
		"ttl"?: number
	}
	=> {
		"response": {
			"record": DomainRecord
		}
	};

route updateDomainRecord(): POST:/domain/updaterecord
	<= {
		"recordid": number,
		"data"?: string,
		"host"?: DomainRecordHost,
		"type"?: DomainRecordType,
		"ttl"?: number
	}
	=> {
		"response": {
			"record": DomainRecord
		}
	};

route deleteDomainRecord(): POST:/domain/deleterecord
	<= {
		"recordid": number
	}
	=> {
		"response": {}
	};

# ==============================================================================
